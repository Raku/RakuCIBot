use Test;
use SerialDedup;

my class A {
    has $.callcount = 0;
    method m() is serial-dedup {
        $!callcount++;
        sleep 0.1;
    }
}


my A $a .= new;
is $a.callcount, 0, 'Not implicitly called';
$a.m;
sleep 0.2;
is $a.callcount, 1, 'Called when called explicitly';

for ^3 { start { $a.m; } }
sleep 0.5;
is $a.callcount, 3, "Colliding calls don't stack up";

{
    my $*SERIAL_DEDUP_NO_THREADING = True;
    $a.m;
    $a.m;
    $a.m;
    is $a.callcount, 6, "Disabling instant return dynamically";
}

for ^3 { start { $a.m; } }
sleep 0.5;
is $a.callcount, 8, "Normal behavior after unsetting the no threading dyn var";

done-testing;

